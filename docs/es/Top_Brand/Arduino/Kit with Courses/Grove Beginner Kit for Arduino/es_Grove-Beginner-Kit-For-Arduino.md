---
description: Grove Kit para Principiantes para Arduino
title: Grove Kit para Principiantes para Arduino
keywords:
- Kit with Courses
image: https://files.seeedstudio.com/wiki/wiki-platform/S-tempor.png
slug: /es/Grove-Beginner-Kit-For-Arduino
last_update:
  date: 2/14/2023
  author: hushuxu
---

# Grove Kit para Principiantes para Arduino

Grove Kit para Principiantes para Arduino es uno de los mejores Kits para Principiantes de Arduino para principiantes. Incluye una Placa compatible con Arduino y 10 sensores adicionales de Arduino, todo en un dise√±o de PCB de una sola pieza. **Todos los m√≥dulos han sido conectados al Seeeduino a trav√©s de los orificios de estampado de PCB, por lo que no se necesitan cables Grove para conectar**. Por supuesto, tambi√©n puedes sacar los m√≥dulos y usar cables Grove para conectar los m√≥dulos. Puedes construir cualquier proyecto de Arduino que desees con este Grove Kit para Principiantes para Arduino.

<div class="get_one_now_container" style={{textAlign: 'center'}}>
    <a class="get_one_now_item" href="https://www.seeedstudio.com/Grove-Beginner-Kit-for-Arduino-p-4549.html" target="_blank">
            <strong><span><font color={'FFFFFF'} size={"4"}> Obtener Uno Ahora üñ±Ô∏è</font></span></strong>
    </a>
</div>

## Descripci√≥n General del Hardware

**Versi√≥n anterior** (Antes de octubre de 2025) ‚Üì

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Parts.jpg" style={{width:1000, height:'auto'}}/></div>

**Nueva Versi√≥n** (Despu√©s de octubre de 2025) ‚Üì

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/1.png" style={{width:1000, height:'auto'}}/></div>

:::tip Notas de Iteraci√≥n de Versi√≥n Nueva y Antigua
Comparado con la versi√≥n anterior, esta nueva versi√≥n actualiza ciertos sensores descontinuados. El sensor de temperatura y humedad DHT11 ha sido reemplazado con el sensor de temperatura y humedad DHT20; el sensor de presi√≥n barom√©trica BMP280 ha sido reemplazado con el sensor de presi√≥n barom√©trica SPA06-003.
:::

**Nota:** Dimensiones - 17.69 *11.64* 1.88cm

1. **[Grove - LED](https://wiki.seeedstudio.com/es/Grove-Red_LED/):** M√≥dulo LED simple
2. **[Grove - Buzzer](https://wiki.seeedstudio.com/es/Grove-Buzzer/):** Zumbador Piezoel√©ctrico
3. **[Grove - Pantalla OLED 0.96"](https://wiki.seeedstudio.com/es/Grove-OLED-Display-0.96-SSD1315/):** Resoluci√≥n de puntos 128√ó64 Alto brillo, auto-emisi√≥n y alta relaci√≥n de contraste Pantalla grande en un dise√±o compacto Bajo consumo de energ√≠a.
4. **[Grove - Bot√≥n](https://wiki.seeedstudio.com/es/Grove-Button/):** Bot√≥n Pulsador Moment√°neo
5. **[Grove - Potenci√≥metro Rotatorio](https://wiki.seeedstudio.com/es/Grove-Rotary_Angle_Sensor/):** Potenci√≥metro Ajustable
6. **[Grove - Luz](https://wiki.seeedstudio.com/es/Grove-Light_Sensor/):** Detecta la intensidad de luz circundante
7. **[Grove - Sonido](https://wiki.seeedstudio.com/es/Grove-Sound_Sensor/):** Detecta la intensidad de sonido circundante
8. **[Grove - Sensor de Temperatura y Humedad](https://wiki.seeedstudio.com/es/Grove-TemperatureAndHumidity_Sensor/):** Detecta los valores de temperatura y humedad circundantes. La versi√≥n anterior usaba el sensor DHT11, mientras que las versiones posteriores a 2025.10 usan el sensor DHT20.
9. **Grove - Sensor de Presi√≥n de Aire:** Detecta la presi√≥n atmosf√©rica circundante

- La versi√≥n anterior usaba el [BMP280](https://wiki.seeedstudio.com/es/Grove-Barometer_Sensor-BMP280/).
- La nueva versi√≥n fechada en octubre de 2025 usa [SPA06-003](https://wiki.seeedstudio.com/es/grove_barometer_sensor_spa06_003).

10. **[Grove - Aceler√≥metro de 3 Ejes](https://wiki.seeedstudio.com/es/Grove-3-Axis-Digital-Accelerometer-LIS3DHTR):** Detecta la aceleraci√≥n del objeto
11. **[Seeeduino Lotus](https://wiki.seeedstudio.com/es/Seeeduino_Lotus/):** Placa Compatible con Arduino con Puertos Grove

**Nota:**
        Por defecto, los m√≥dulos Grove est√°n conectados al Seeeduino a trav√©s de orificios de estampado de PCB. Esto significa que no necesitas usar cables Grove para conectar si no est√°n separados. Los pines por defecto son los siguientes:

|M√≥dulos|Interfaz|Pines/Direcci√≥n|
|---|---|---|
|LED|Digital|D4|
|Buzzer|Digital|D5|
|Pantalla OLED 0.96"|I2C|I2C, 0x78(por defecto)|
|Bot√≥n|Digital|D6|
|Potenci√≥metro Rotatorio|Anal√≥gico|A0|
|Luz|Anal√≥gico|A6|
|Sonido|Anal√≥gico|A2|
|Sensor de Temperatura y Humedad|Digital|D3|
|Sensor de Presi√≥n de Aire|I2C|I2C, 0x77(por defecto) / 0x76(opcional)|
|Aceler√≥metro de 3 Ejes|I2C|I2C, 0x19(por defecto)|

### Instrucci√≥n de Separaci√≥n

**Atenci√≥n:**
    Ten cuidado de no cortarte las manos al usar un cuchillo

Si prefieres usar los m√≥dulos en otro lugar, entonces puedes simplemente seguir los procedimientos para separar los m√≥dulos.

**Paso 1**

Usa un cuchillo o un objeto afilado para cortar en los orificios de perforaci√≥n que conectan los sensores entre s√≠

**Paso 2**

Agita los m√≥dulos hacia arriba y hacia abajo y deber√≠an salir bastante f√°cilmente.

## Lista de Partes

|M√≥dulos|Cantidad|
|---|---|
|**Sensores**||
|Sensores de Temperatura y Humedad|x1|
|Aceler√≥metros de 3 Ejes|x1|
|Presi√≥n de Aire|x1|
|Sensor de Luz|x1|
|Sensor de Sonido|x1|
|**M√≥dulos de Entrada**||
|Potenci√≥metro Rotativo|x1|
|Bot√≥n|x1|
|**M√≥dulos de Salida**||
|LED|x1|
|Zumbador|x1|
|**M√≥dulo de Pantalla**||
|Pantalla OLED|x1|
|**Cables Grove**|x6|
|**Cable Micro USB**|x1|

## Objetivos de Aprendizaje

- Fundamentos de Sistemas de Hardware de C√≥digo Abierto.
- Programaci√≥n B√°sica de Arduino.
- Principios y m√©todos de comunicaci√≥n para sensores.
- Implementaci√≥n pr√°ctica de proyectos de Hardware de C√≥digo Abierto.

### Demo de Desempaquetado Plug and Play

El Kit para Principiantes Grove tiene un demo de desempaquetado plug and play, donde primero conectas la alimentaci√≥n a la placa, ¬°obtienes la oportunidad de experimentar todos los sensores de una vez! ¬°Usa el bot√≥n y el potenci√≥metro rotativo para experimentar cada demo de sensor!

![](https://s3-us-west-2.amazonaws.com/files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Firmware.jpg)

- **Desplazar** -> Rotar Potenci√≥metro Rotativo
- **Seleccionar** -> Presi√≥n Corta del Bot√≥n
- **Salir del Demo Actual** -> Presi√≥n Larga del Bot√≥n

El m√≥dulo de zumbador y LED se utilizan para la indicaci√≥n de teclas.

## C√≥mo Empezar Con Arduino

### Instalar el IDE de Arduino

- **Arduino IDE** es un entorno de desarrollo integrado para Arduino, que se utiliza para programaci√≥n de software de microcontroladores de un solo chip, descarga, pruebas y dem√°s.
- Descarga e Instala [Arduino IDE](https://www.arduino.cc/en/Main/Software) para tu sistema operativo deseado aqu√≠.

![](https://files.seeedstudio.com/wiki/Seeeduino_Stalker_V3_1/images/Download_IDE.png)

### Instalar el controlador USB

- Arduino se conecta a la PC a trav√©s de un cable USB. El controlador USB depende del tipo de chip USB que est√©s usando en tu Arduino. *Nota: Los chips USB generalmente est√°n impresos en la parte posterior de la placa de desarrollo.*

  - Descarga el [Controlador USB CP2102](https://www.silabs.com/products/development-tools/software/usb-to-uart-bridge-vcp-drivers). **Nota:** Descarga seg√∫n tu SO.
  - Despu√©s de que se complete la instalaci√≥n del controlador, conecta Arduino al puerto USB de la PC con un cable USB.
    - **Para usuarios de Windows:** Puedes verlo en `Mi PC` -> `Propiedades` -> `Hardware` -> `Administrador de Dispositivos`. Aparecer√° un `COM`.
    - **Para usuarios de Mac OS:** Puedes navegar a `` en la esquina superior izquierda, y elegir `Acerca de esta Mac` -> `Informe del Sistema...` -> `USB`. Deber√≠a aparecer un Controlador USB CP2102.
  - Si el controlador no est√° instalado, o si el controlador est√° instalado incorrectamente (no coincide con el modelo del chip), aparecer√° como un "dispositivo desconocido" en el administrador de dispositivos. En este punto, el controlador debe reinstalarse.

### Iniciar el IDE de Arduino

1.Abre el **IDE de Arduino** en tu PC.
2.Haz clic en `Herramientas` -> `Placa`-> `Placas Arduino AVR`-> `Arduino Uno` para seleccionar el Modelo de Placa de Desarrollo correcto. Selecciona **Arduino Uno** como Placa.

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/uno_board.jpg)

3.Haz clic en `Herramientas` -> `Puerto` para seleccionar el Puerto correcto (el Puerto Serie mostrado en el Administrador de Dispositivos en el paso anterior). En este caso, se selecciona `COM11`. **Para usuarios de Mac OS**, deber√≠a ser `/dev/cu.SLAB_USBtoUART`.

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/port.jpg)

4.Crea un nuevo archivo de Arduino y n√≥mbralo `Hello.ino`, luego copia el siguiente c√≥digo en √©l:

```Cpp
void setup() {
  Serial.begin(9600); // initializes the serial port with a baud rate of 9600
}
void loop() {
  Serial.println("hello, world"); // prints a string to a serial port
  delay(1000); //delay of 1 second
}
```

5.En la esquina superior izquierda del IDE de Arduino, hay dos botones, **Verificar y Cargar**. Primero, presiona el bot√≥n Verificar(‚úì) para compilar. Despu√©s de que la compilaci√≥n sea exitosa, presiona el bot√≥n de carga(‚Üí).

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/func.png)

6.Navega a `Tools` -> `Serial Monitor`, o haz clic en el **Serial Monitor** en la esquina superior derecha(S√≠mbolo de Lupa), puedes ver los resultados de ejecuci√≥n del programa:

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/print.png)

**Nota:** Si instalaste el IDE de Arduino port√°til desde nuestro USB Drive, puedes encontrar todas las demostraciones de m√≥dulos en **Files** -> **Sketch Book**, as√≠ como todas las librer√≠as de m√≥dulos, est√°n preinstaladas con el IDE de Arduino!

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Sketchbook.png" style={{width:600, height:'auto'}}/></div>

**Nota:**
        Todos los m√≥dulos est√°n precableados en una sola placa de circuito, por lo que no se necesitan cables ni soldadura. Sin embargo, si separas los m√≥dulos y quieres conectarlos con cables Grove, por favor revisa amablemente la Gu√≠a de Separaci√≥n.

## Gu√≠a de Lecciones

### Lecci√≥n 1: Parpadeando con el LED

Hemos completado el programa de salida "Hello world". Ahora aprendamos c√≥mo encender el m√≥dulo LED. Conocemos los tres componentes b√°sicos de un sistema de control: Entrada, Control y Salida. Pero encender el LED usa solo la salida, no la entrada. Seeeduino es la unidad de control, el m√≥dulo LED es la unidad de salida y la se√±al de salida es una se√±al digital.

Informaci√≥n de Contexto:

- **¬øQu√© es una Se√±al Digital?**

**Se√±al digital:** La se√±al digital se refiere al valor de la amplitud que es discreto, la amplitud est√° limitada a un n√∫mero finito de valores. En nuestro controlador, la se√±al digital tiene dos estados: LOW(0V) para 0; HIGH(5V) para 1. As√≠ que enviar una se√±al HIGH al LED puede encenderlo.

![Alt text](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/digital.png)

Componentes Involucrados
    1. Seeeduino Lotus
    2. Grove LED
    3. Grove Cable(Si est√° Separado)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/LED.png)

Conexi√≥n de hardware
    - **Conexi√≥n de m√≥dulo**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - Conecta el Seeeduino a la computadora a trav√©s del cable USB.

-C√≥digo de Software

    - Abre el IDE de Arduino.
    - Copia el siguiente c√≥digo, haz clic en Verificar para revisar errores de sintaxis. Verifica que no haya errores, y puedes cargar el c√≥digo.

```Cpp
//LED Blink
//The LED will turn on for one second and then turn off for one second
int ledPin = 4;
void setup() {
    pinMode(ledPin, OUTPUT);
}
void loop() {
    digitalWrite(ledPin, HIGH);
    delay(1000);
    digitalWrite(ledPin, LOW);
    delay(1000);
}
```

An√°lisis de C√≥digo

```cpp
setup(){
}
```

La funci√≥n `setup()` se llama cuando un sketch inicia. √ösala para inicializar variables, modos de pines, comenzar a usar librer√≠as, etc. La funci√≥n `setup()` solo se ejecutar√° una vez, despu√©s de cada encendido o reinicio de la placa Arduino.

```cpp
loop(){
}
```

Despu√©s de crear una funci√≥n `setup()`, que inicializa y establece los valores iniciales, la funci√≥n `loop()` hace precisamente lo que su nombre sugiere, y se ejecuta en bucle consecutivamente, permitiendo que tu programa cambie y responda. √ösala para controlar activamente la placa Arduino.

```cpp
int ledPin = 4;
```

**Descripci√≥n:**

Convierte un valor al tipo de datos int.

**Sintaxis:**

int(**x**) o (int)**x** (conversi√≥n de tipo estilo C)

**Par√°metros:**

**x**: un valor. Tipos de datos permitidos: cualquier tipo.

Asigna un tipo `int` 4 a la variable llamada ledPin.

```cpp
pinMode(ledPin, OUTPUT);
```

**Descripci√≥n:**

Configura el pin especificado para comportarse como una entrada o una salida. Consulta la p√°gina de Pines Digitales para obtener detalles sobre la funcionalidad de los pines.

A partir de Arduino 1.0.1, es posible habilitar las resistencias pull-up internas con el modo `INPUT_PULLUP`. Adem√°s, el modo `INPUT` desactiva expl√≠citamente las resistencias pull-up internas.

**Sintaxis:**

pinMode(**pin, mode**)

**Par√°metros:**

**pin**: el n√∫mero de pin de Arduino para establecer el modo.

**mode**: `INPUT`, `OUTPUT`, o `INPUT_PULLUP`.

Configurando ledPin al modo de salida.

```cpp
digitalWrite(ledPin, HIGH);
```

**Descripci√≥n:**

Escribe un valor `HIGH` o `LOW` a un pin digital.

Si el pin ha sido configurado como OUTPUT con pinMode(), su voltaje se establecer√° al valor correspondiente: 5V (o 3.3V en placas de 3.3V) para `HIGH`, 0V (tierra) para `LOW`.

Si el pin est√° configurado como INPUT, digitalWrite() habilitar√° (HIGH) o deshabilitar√° (LOW) la resistencia pull-up interna en el pin de entrada. Se recomienda establecer el pinMode() a `INPUT_PULLUP` para habilitar la resistencia pull-up interna. Consulta el tutorial de Pines Digitales para m√°s informaci√≥n.

Si no estableces el pinMode() a OUTPUT, y conectas un LED a un pin, al llamar digitalWrite(HIGH), el LED puede aparecer tenue. Sin establecer expl√≠citamente pinMode(), digitalWrite() habr√° habilitado la resistencia pull-up interna, que act√∫a como una gran resistencia limitadora de corriente.

**Sintaxis:**

digitalWrite(**pin, value**)

**Par√°metros:**

**pin**: el n√∫mero de pin de Arduino.

**value**: `HIGH` o `LOW`.

Cuando establecemos el ledPin como salida, HIGH significa enviar nivel alto al pin, el LED se enciende.

```cpp
digitalWrite(ledPin, LOW);
```

Cuando configuramos el LED como salida, LOW significa enviar un nivel bajo al pin, el LED se apaga.

```cpp
delay(1000);
```

**Descripci√≥n:**

Pausa el programa durante la cantidad de tiempo (en milisegundos) especificada como par√°metro. (Hay 1000 milisegundos en un segundo.)

**Sintaxis:**

delay(**ms**)

**Par√°metros:**

**ms**: el n√∫mero de milisegundos a pausar. Tipos de datos permitidos: unsigned long.

Retrasar el programa por 1000ms(1s).

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serial:**

El m√≥dulo LED estar√° 1 segundo encendido y 1 segundo apagado.

**Ajuste de Brillo del LED:**

<div>
  <div align="center"><img src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/LED-res.jpeg" /></div>
  <br />
</div>

En el m√≥dulo LED Grove, hay una **resistencia variable que se puede ajustar usando un destornillador.** ¬°G√≠rala para hacer que el LED se ilumine m√°s brillante!

Gu√≠a de Separaci√≥n

Si los m√≥dulos se separan de la placa. Usa un cable Grove para conectar el **LED Grove** a la interfaz digital **D4** del Seeeduino Lotus.

### Lecci√≥n 2: Presionar Bot√≥n para Encender LED

Lo primero que necesitamos saber es que la entrada del bot√≥n es una se√±al digital, y solo hay dos estados, 0 o 1, por lo que podemos controlar la salida bas√°ndonos en esos dos estados.

- **Pr√°ctica:** Usar bot√≥n para ENCENDER y APAGAR el m√≥dulo LED

Componentes Involucrados

    1. Seeeduino Lotus
    2. LED Grove
    3. Bot√≥n Grove
    4. Cables Grove(Si est√°n separados)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Button.png)

Conexi√≥n de hardware

    - **Conexi√≥n de m√≥dulos:**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora mediante un cable USB.

- **An√°lisis de hardware**:
  - Entrada: Bot√≥n
  - Control: Seeeduino
  - Salida: m√≥dulo LED

Tanto el sensor como el LED usan se√±ales digitales, por lo que deben conectarse a interfaces digitales.

- **C√≥digo de software**:
  - Abrir Arduino IDE.
  - Copiar el siguiente c√≥digo, hacer clic en Verificar para comprobar errores de sintaxis. Verificar que no hay errores, y puedes subir el c√≥digo.

```Cpp
//Button to turn ON/OFF LED
//Constants won't change. They're used here to set pin numbers:
const int buttonPin = 6;     // the number of the pushbutton pin
const int ledPin =  4;      // the number of the LED pin

// variables will change:
int buttonState = 0;         // variable for reading the pushbutton status

void setup() {
  // initialize the LED pin as an output:
  pinMode(ledPin, OUTPUT);
  // initialize the pushbutton pin as an input:
  pinMode(buttonPin, INPUT);
}

void loop() {
  // read the state of the pushbutton value:
  buttonState = digitalRead(buttonPin);

  // check if the pushbutton is pressed. If it is, the buttonState is HIGH:
  if (buttonState == HIGH) {
    // turn LED on:
    digitalWrite(ledPin, HIGH);
  } else {
    // turn LED off:
    digitalWrite(ledPin, LOW);
  }
}
```

An√°lisis del C√≥digo

```cpp
pinMode(ledPin, OUTPUT);
```

Define el LED como la unidad de salida.

```cpp
pinMode(buttonPin, INPUT);
```

Define el bot√≥n como la unidad de entrada.

```cpp
buttonState = digitalRead(buttonPin);
```

**Descripci√≥n:**

Lee el valor de un pin digital especificado, ya sea `HIGH` o `LOW`.

**Sintaxis:**

digitalRead(**pin**)

**Par√°metros:**

**pin**: el n√∫mero de `pin` de Arduino que quieres leer

Esta funci√≥n se utiliza para leer los estados de los pines digitales, ya sea HIGH o LOW. Cuando se presiona el bot√≥n, el estado es HIGH, de lo contrario es LOW.

```cpp
  if (buttonState == HIGH) {
    digitalWrite(ledPin, HIGH);
  } else {
    digitalWrite(ledPin, LOW);
  }
}
```

**Descripci√≥n:**

El if‚Ä¶‚Äãelse permite un mayor control sobre el flujo del c√≥digo que la declaraci√≥n if b√°sica, al permitir que m√∫ltiples pruebas se agrupen. Una cl√°usula else (si existe) se ejecutar√° si la condici√≥n en la declaraci√≥n if resulta en falso. El else puede preceder a otra prueba if, de modo que m√∫ltiples pruebas mutuamente excluyentes pueden ejecutarse al mismo tiempo.

Cada prueba proceder√° a la siguiente hasta que se encuentre una prueba verdadera. Cuando se encuentra una prueba verdadera, se ejecuta su bloque de c√≥digo asociado, y el programa luego salta a la l√≠nea que sigue a toda la construcci√≥n if/else. Si ninguna prueba resulta ser verdadera, se ejecuta el bloque else predeterminado, si est√° presente, y establece el comportamiento predeterminado.

Tenga en cuenta que un bloque else if puede usarse con o sin un bloque else de terminaci√≥n y viceversa. Se permite un n√∫mero ilimitado de tales ramas else if.

**Sintaxis:**

```cpp
if (condition1) {
  // do Thing A
}
else if (condition2) {
  // do Thing B
}
else {
  // do Thing C
}
```

El uso de la declaraci√≥n es: si la expresi√≥n l√≥gica entre par√©ntesis es verdadera, ejecuta la declaraci√≥n entre llaves despu√©s de **if**, si no, ejecuta la declaraci√≥n entre llaves despu√©s del **else**. Si el estado del bot√≥n es alto, el pin LED emite un nivel alto y enciende el LED, de lo contrario apaga el LED.

**Efecto de la Demostraci√≥n y Resultado de Impresi√≥n Serial:**

Presionar el bot√≥n encender√° el m√≥dulo LED.

Gu√≠a de Conexi√≥n

Usa un cable Grove para conectar el LED Grove a la interfaz digital **D4** del Seeeduino Lotus. Conecta el Bot√≥n Grove a la interfaz digital **D6**.

### Lecci√≥n 3: Controlando la Frecuencia del Parpadeo

En la secci√≥n anterior, estudiamos que el bot√≥n solo tiene dos estados, estado ON/OFF correspondiente a 0V y 5V, pero en la pr√°ctica, a menudo encontramos la necesidad de muchos estados, no solo 0V y 5V. ¬°Entonces necesitas usar Se√±al Anal√≥gica! El Potenci√≥metro Rotatorio es un ejemplo cl√°sico que usa una se√±al anal√≥gica.

Informaci√≥n de Contexto:

- **Qu√© es una Se√±al Anal√≥gica**

**Se√±ales anal√≥gicas:** Las se√±ales var√≠an continuamente en tiempo y valor, y la amplitud, frecuencia o fase de la se√±al cambia continuamente en cualquier momento, como la se√±al de sonido de transmisi√≥n actual, o se√±al de imagen, etc. La se√±al anal√≥gica tiene onda senoidal y onda triangular y as√≠ sucesivamente. Los pines anal√≥gicos de tu microcontrolador pueden tener entre 0V y 5V mapeados a un rango entre 0 y 1023 donde 1023 est√° mapeado como 5V y 512 est√° mapeado como 2.5v y etc.

![Alt text](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/analog.png)

Componentes Involucrados
    1. Seeeduino Lotus
    2. LED Grove
    3. Interruptor Rotatorio Grove
    4. Cables Grove(Si est√°n separados)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/rotary.png)

Conexi√≥n de hardware
    - **Conexi√≥n del m√≥dulo:**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora a trav√©s de un cable USB.

- **An√°lisis de hardware:**

  - Entrada: Potenci√≥metro Rotatorio
  - Control: Seeeduino Lotus
  - Salida: m√≥dulo LED

La entrada es una se√±al anal√≥gica, por lo que est√° conectada a la interfaz de se√±al anal√≥gica, el m√≥dulo LED est√° conectado a la interfaz de se√±al digital.

Software

    - Abre Arduino IDE.
    - Copia el siguiente c√≥digo, haz clic en Verificar para comprobar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.

```Cpp
//Rotary controls LED
int rotaryPin = A0;    // select the input pin for the rotary
int ledPin = 4;      // select the pin for the LED
int rotaryValue = 0;  // variable to store the value coming from the rotary

void setup() {
  // declare the ledPin as an OUTPUT:
  pinMode(ledPin, OUTPUT);
  pinMode(rotaryPin, INPUT);
}

void loop() {
  // read the value from the sensor:
  rotaryValue = analogRead(rotaryPin);
  // turn the ledPin on
  digitalWrite(ledPin, HIGH);
  // stop the program for <sensorValue> milliseconds:
  delay(rotaryValue);
  // turn the ledPin off:
  digitalWrite(ledPin, LOW);
  // stop the program for for <sensorValue> milliseconds:
  delay(rotaryValue);
}
```

An√°lisis del C√≥digo

```cpp
int rotaryPin = A0;    // select the input pin for the rotary
int ledPin = 4;      // select the pin for the LED
```

**Descripci√≥n:**

Puede que encuentres que definimos rotatePin y ledPin de diferentes maneras. Esto es porque el Potenci√≥metro Rotatorio genera una se√±al anal√≥gica, y el LED es controlado por una se√±al digital.

Para **definir un Pin Anal√≥gico**, usa A + el n√∫mero del Pin (Por ejemplo aqu√≠ `A0`).

Para **definir un Pin Digital**, usa solo el n√∫mero del pin (Por ejemplo aqu√≠ `4`).

```cpp
rotaryValue = analogRead(rotaryPin);
```

**Descripci√≥n:**

Lee el valor del pin anal√≥gico especificado. Las placas Arduino contienen un convertidor anal√≥gico a digital multicanal de 10 bits. Esto significa que mapear√° voltajes de entrada entre 0 y el voltaje de operaci√≥n (5V o 3.3V) en valores enteros entre 0 y 1023. En un Arduino UNO, por ejemplo, esto produce una resoluci√≥n entre lecturas de: 5 voltios / 1024 unidades o, 0.0049 voltios (4.9 mV) por unidad.

**Sintaxis:**

analogRead(**pin**)

**Par√°metros:**

**pin**: el nombre del pin de entrada anal√≥gica del cual leer (A0 a A5 en la mayor√≠a de las placas).

**Devuelve:** La lectura anal√≥gica en el pin. Aunque est√° limitada a la resoluci√≥n del convertidor anal√≥gico a digital (0-1023 para 10 bits o 0-4095 para 12 bits). Tipo de dato: int.

Esta funci√≥n se usa para leer el valor de los pines anal√≥gicos (la posici√≥n del sensor rotativo), el rango de valores es: 0 ~ 1023.

```cpp
delay(rotaryValue);
```

Funci√≥n de retardo, La duraci√≥n en milisegundos del retardo es el valor entre par√©ntesis. Debido a que el valor es el valor de la se√±al anal√≥gica del pin de la perilla que se est√° leyendo, el tiempo de retardo puede ser controlado por la perilla.

**Efecto de la Demostraci√≥n y Resultado de Impresi√≥n Serie:**

Girar el Potenci√≥metro cambiar√° la frecuencia del parpadeo del LED.

Gu√≠a de Conexi√≥n

Usa un cable Grove para conectar el LED a la interfaz digital **D4** del Seeeduino Lotus, y un cable Grove para conectar el Grove Rotary Switch a la interfaz de se√±al anal√≥gica **A0**.

### Lecci√≥n 4: Haciendo que el Zumbador haga BEEP

Al igual que el m√≥dulo LED, el Zumbador tambi√©n es un m√≥dulo de salida, en lugar de encenderse produce un sonido de pitido. Esto puede ser usado para muchas situaciones con prop√≥sitos de indicaci√≥n. ¬°Aprendamos c√≥mo generar sonido usando el zumbador!

Informaci√≥n de Contexto:

- **¬øCu√°l es la diferencia entre Zumbador Activo y Pasivo?**

 Hay dos tipos de zumbadores, uno es **activo** y el otro es **pasivo**. Tanto los zumbadores activos como pasivos se usan para hacer sonido en la electr√≥nica.

  El **zumbador activo** tiene una fuente de oscilaci√≥n interna que hace que el zumbador suene cada vez que se aplica energ√≠a. Los zumbadores activos son ampliamente usados en computadoras, impresoras, copiadoras, alarmas, juguetes electr√≥nicos, electr√≥nica automotriz, tel√©fonos, temporizadores y otros dispositivos de sonido de productos electr√≥nicos.

  Un **zumbador pasivo** no tiene fuente interna de oscilaci√≥n y necesita ser controlado por **una onda cuadrada** y **una frecuencia diferente**. Act√∫a como un altavoz electromagn√©tico, y la se√±al de entrada cambiante produce sonido, en lugar de un tono autom√°ticamente.

 <div align="center"><img width={500} src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/buzzer-ap.jpg" /></div>

En este kit, el **Grove-Buzzer es un zumbador pasivo** por lo que necesita una se√±al AC para controlarlo. Esto entonces lleva a la siguiente pregunta, ¬°c√≥mo generar Onda Cuadrada (se√±ales AC) con Arduino! Bueno, una manera f√°cil es usar PWM.

- **¬øQu√© es PWM?**

**Modulaci√≥n por Ancho de Pulso, o PWM**, es una t√©cnica para obtener resultados anal√≥gicos con medios digitales. El control digital se usa para crear una onda cuadrada, una se√±al conmutada entre encendido y apagado. Este patr√≥n de encendido-apagado puede simular voltajes entre completamente encendido (5 Voltios) y apagado (0 Voltios) cambiando la porci√≥n del tiempo que la se√±al pasa encendida versus el tiempo que la se√±al pasa apagada. La duraci√≥n del "tiempo encendido" se llama el ancho de pulso. Para obtener valores anal√≥gicos variables, cambias, o modulas, ese ancho de pulso. Si repites este patr√≥n de encendido-apagado lo suficientemente r√°pido, el resultado es como si la se√±al fuera un voltaje constante entre 0 y 5v como una se√±al AC. *Referencia: [Arduino](https://www.arduino.cc/en/tutorial/PWM)*. Esta se√±al PWM puede entonces ser usada para controlar el zumbador pasivo con facilidad.

Para generar se√±ales PWM en Arduino, puedes usar **`analogWrite()`**, en contraste con usar `digitalWrite()` para generar se√±ales DC.

Hay seis pines digitales en tu Seeeduino que est√°n marcados con el s√≠mbolo "~", lo que significa que pueden enviar una se√±al PWM: 3,5,6,9,10,11. Se llaman pines PWM.

Componentes Involucrados

    1. Seeeduino Lotus
    2. Grove Buzzer
    3. Grove Cable(Si est√° Separado)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Buzzer.png)

Conexi√≥n de Hardware

    - **Conexi√≥n del m√≥dulo**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - Conecta el Seeeduino a la computadora a trav√©s del cable USB.

C√≥digo de Software

    - Abre Arduino IDE.
    - Copia el siguiente c√≥digo, haz clic en Verificar para comprobar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.

```Cpp
int BuzzerPin = 5;

void setup() {
  pinMode(BuzzerPin, OUTPUT);
}

void loop() {
  analogWrite(BuzzerPin, 128);
  delay(1000);
  analogWrite(BuzzerPin, 0);
  delay(0);
}
```

An√°lisis de C√≥digo

```cpp
analogWrite(BuzzerPin, 128);
```

**Descripci√≥n:**

Escribe un valor anal√≥gico (onda PWM) a un pin. Puede usarse para encender un LED con diferentes brillos o mover un motor a varias velocidades. Despu√©s de una llamada a analogWrite(), el pin generar√° una onda rectangular constante del ciclo de trabajo especificado hasta la siguiente llamada a analogWrite() (o una llamada a digitalRead() o digitalWrite()) en el mismo pin.

**Sintaxis:**

analogWrite(**pin, value**)

**Par√°metros:**

**pin**: el `pin` de Arduino al que escribir. Tipos de datos permitidos: int.

**value**: el ciclo de trabajo: entre `0` (siempre apagado) y `255` (siempre encendido). Tipos de datos permitidos: int.

Escribe un valor anal√≥gico (onda PWM) al Zumbador.

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serial:**

El Zumbador suena.

Gu√≠a de Conexi√≥n

Usa un cable Grove para conectar el Zumbador Grove a la interfaz digital **D5** del Seeeduino Lotus.

-----

Uso de PWM

Ahora que hemos aprendido el uso de PWM, adem√°s de usar PWM para controlar el zumbador pasivo, tambi√©n podemos usar PWM para controlar la velocidad del motor y **el brillo de las luces LED** y etc.

Como indica el diagrama a continuaci√≥n, usa `analogWrite()` para generar ondas PWM, mientras mayor sea el porcentaje del Ciclo de Trabajo, m√°s brillante ser√° el LED.

<div align="center"><img src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/PWM-LED.png" /></div>

Sin embargo, el M√≥dulo LED en el Kit para Principiantes Grove no puede ser controlado directamente por PWM, porque el m√≥dulo LED est√° conectado a D4, y como se mencion√≥ anteriormente, los pines PWM son 3, 5, 6, 9, 10, 11, y el pin 4 no es un pin PWM. Si quieres controlar el LED con PWM, necesitas desconectarlo y usar el cable Grove para conectar al puerto Grove con funci√≥n PWM.

Por ejemplo, conectemos **Grove-LED a D3 usando un cable Grove**:

!!!Note
    D3 tambi√©n est√° interconectado al Sensor de Temperatura y Humedad Grove, y por lo tanto este ejemplo no puede usarse junto con el Sensor de Temperatura y Humedad Grove.

<div align="center"><img src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/pwmled-connect.png" /></div>

```cpp
int LED = 3; // Cable connection from LED to D3
int Potentiometer = A0;

void setup() {
  pinMode(LED, OUTPUT);
  pinMode(Potentiometer, INPUT);
}

void loop() {
  int potentioValue, Value;
  potentioValue = analogRead(Potentiometer);
  Value = map(potentioValue, 0, 1023, 0, 255); //Mapping potentiometer value to PWM signal value
  analogWrite(LED, Value);
}
```

**Compila y sube** el c√≥digo, ¬°deber√≠as poder controlar y ajustar el brillo del LED usando se√±ales PWM!

An√°lisis del C√≥digo

```cpp
Value = map(potentioValue, 0, 1023, 0, 255);
```

**Descripci√≥n:**

Re-mapea un n√∫mero de un rango a otro. Es decir, un valor de **fromLow** se mapear√≠a a **toLow**, un valor de **fromHigh** a **toHigh**, valores intermedios a valores intermedios, etc.

No restringe valores dentro del rango, porque los valores fuera del rango a veces son intencionados y √∫tiles. La funci√≥n `constrain()` puede usarse antes o despu√©s de esta funci√≥n, si se desean l√≠mites a los rangos.

Nota que los "l√≠mites inferiores" de cualquier rango pueden ser mayores o menores que los "l√≠mites superiores", por lo que la funci√≥n `map()` puede usarse para invertir un rango de n√∫meros, por ejemplo

**y = map(x, 1, 50, 50, 1);**

La funci√≥n tambi√©n maneja bien los n√∫meros negativos, por lo que este ejemplo

**y = map(x, 1, 50, 50, -100);**

tambi√©n es v√°lido y funciona bien.

La funci√≥n map() usa matem√°ticas enteras por lo que no generar√° fracciones, cuando las matem√°ticas podr√≠an indicar que deber√≠a hacerlo. Los residuos fraccionarios se truncan y no se redondean o promedian.

**Sintaxis:**

map(**value, fromLow, fromHigh, toLow, toHigh**)

**Par√°metros:**

**value**: el n√∫mero a mapear.

**fromLow**: el l√≠mite inferior del rango actual del valor.

**fromHigh**: el l√≠mite superior del rango actual del valor.

**toLow**: el l√≠mite inferior del rango objetivo del valor.

**toHigh**: el l√≠mite superior del rango objetivo del valor.

Mapeando la se√±al anal√≥gica del sensor potenci√≥metro (0 a 1023) al volumen de la Luz (0 a 255).

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serial:**

Ajusta el potenci√≥metro para ajustar el brillo del LED.

En resumen, cuando quieras usar la funci√≥n PWM, aseg√∫rate de seleccionar aquellos pines con un s√≠mbolo "~" delante de sus nombres.

### Lecci√≥n 5: Haciendo un LED de Inducci√≥n de Luz

El sensor de luz contiene una resistencia fotosensible para medir la intensidad de la luz. La resistencia de la resistencia fotosensible disminuye con el aumento de la intensidad de la luz. El LED se encender√° si el entorno est√° oscuro, y permanecer√° apagado si el entorno est√° brillante.

En las siguientes secciones, usaremos el Monitor Serial para observar resultados de nuestros sensores, ¬°as√≠ que aqu√≠ viene la breve introducci√≥n!

Informaci√≥n de Fondo:

- **Qu√© es el Monitor Serial**

El Monitor Serial es una herramienta √∫til para observar resultados en Arduino, puede ser muy √∫til en t√©rminos de imprimir resultados de los sensores o depurar en general. ¬°Tambi√©n puedes enviar datos de vuelta al controlador a trav√©s del monitor serial para hacer ciertas tareas! Nota: Aseg√∫rate de que la transferencia de datos Serial coincida con el c√≥digo.

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Serial.jpg)

Puedes abrir el Monitor Serial haciendo clic en **Tools** -> **Serial Monitor**.

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/20200217144001.jpg)

Componentes Involucrados

    1. Seeeduino Lotus
    2. Grove LED
    3. Grove Sensor de Luz
    4. Grove Cable(Si est√° separado)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Light.png)

Conexi√≥n de hardware

    - **Conexi√≥n del m√≥dulo:**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora a trav√©s de un cable USB.

- **An√°lisis de hardware:**

  - Entrada: Sensor de Luz
  - Control: Seeeduino Lotus
  - Salida: m√≥dulo LED

C√≥digo de Software

    - Abre Arduino IDE.
    - Copia el siguiente c√≥digo, haz clic en Verificar para comprobar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.

```Cpp
// Light Switch
int sensorpin = A6; // Analog input pin that the sensor is attached to
int ledPin = 4;    // LED port
int sensorValue = 0;        // value read from the port
int outputValue = 0;        // value output to the PWM (analog out)

void setup() {
  pinMode(ledPin,OUTPUT);
  pinMode(sensorpin, INPUT);
  Serial.begin(9600);
}

void loop() {
  // read the analog in value:
  sensorValue = analogRead(sensorpin);

  Serial.println(sensorValue);

  if (sensorValue < 200) {
    digitalWrite(ledPin, HIGH);
  }
  else {
    digitalWrite(ledPin, LOW);
  }

  delay(200);
}
```

Tambi√©n puedes ver las lecturas de intensidad de luz desde el **Monitor Serie**, navega a **Herramientas** -> **Monitor Serie**.

An√°lisis de C√≥digo

```cpp
Serial.begin(9600);
```

**Descripci√≥n:**

Establece la velocidad de datos en bits por segundo (baudios) para la transmisi√≥n de datos serie. Para comunicarse con el Monitor Serie, aseg√∫rate de usar una de las velocidades de baudios listadas en el men√∫ de la esquina inferior derecha de su pantalla. Sin embargo, puedes especificar otras velocidades - por ejemplo, para comunicarte a trav√©s de los pines 0 y 1 con un componente que requiere una velocidad de baudios particular.

Un segundo argumento opcional configura los bits de datos, paridad y parada. El valor predeterminado es 8 bits de datos, sin paridad, un bit de parada.

El software que se ejecuta en la computadora se comunica con la placa de desarrollo, y la velocidad de baudios es 9600.

**Sintaxis:**

Serial.begin(**speed**)

**Par√°metros:**

**speed**: Velocidad de comunicaci√≥n Serie. es decir `9600`, `115200` y etc.

Establece la velocidad de baudios Serie a 9600.

```cpp
Serial.println(sensorValue);
```

**Descripci√≥n:**

Imprime datos al puerto serie como texto ASCII legible por humanos seguido de un car√°cter de retorno de carro (ASCII 13, o '\r') y un car√°cter de nueva l√≠nea (ASCII 10, o '\n'). Este comando toma las mismas formas que Serial.print().

**Sintaxis:**

Serial.println(**val**) o Serial.println(**val**, **format**)

**Par√°metros:**

**val**: el valor a imprimir. Tipos de datos permitidos: cualquier tipo de dato.

**format**: especifica la base num√©rica (para tipos de datos integrales) o el n√∫mero de lugares decimales (para tipos de punto flotante).

El puerto serie imprime el valor del sensor de luz. As√≠ que abres el **monitor serie** en la interfaz del IDE, y ves el valor del sensor de salida.

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serie:**

El m√≥dulo LED se encender√° si est√° oscuro y permanecer√° apagado si est√° brillante.

Gu√≠a de Conexi√≥n

Usa el Cable Grove para conectar el Grove LED a la interfaz de se√±al digital **D4** del Seeeduino Lotus, conecta el Grove Sensor de Luz a la interfaz de se√±al anal√≥gica **A6** del Seeeduino Lotus.

### Lecci√≥n 6: Luz LED Sensible al Sonido

El sensor de sonido puede detectar la intensidad del sonido del ambiente, y su salida tambi√©n es simulada. Estoy seguro de que todos han estado expuestos a las luces de control de sonido, pero ahora podemos hacer una nosotros mismos, y con los fundamentos, este experimento ser√° f√°cil para ustedes. Aqu√≠ se us√≥ el Trazador Serie para visualizar los resultados.

Informaci√≥n de Contexto:

- **Qu√© es el Trazador Serie**

El Trazador Serie es similar al Monitor Serie, permiti√©ndote graficar nativamente datos serie desde tu Arduino a tu computadora en tiempo real. Esto es muy √∫til cuando los datos necesitan ser visualizados.

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/SerialPlotter.png)

Puedes abrir el Trazador Serie haciendo clic en **Herramientas** -> **Trazador Serie**.

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/serialplot.jpg" style={{width:400, height:'auto'}}/></div>

- **Pr√°ctica:** Las luces LED se encienden cuando se hace sonido. Cuando no hay sonido y est√° muy silencioso, las luces LED se apagan.

Componentes Involucrados

    1. Seeeduino Lotus
    2. Grove LED
    3. Grove Sensor de Sonido
    4. Cable Grove(Si est√° separado)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Sound.png)

Conexi√≥n de Hardware

    - **Conexi√≥n de m√≥dulos:**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora a trav√©s de un cable USB.

C√≥digo de Software

    - Abre Arduino IDE.
    - Copia el siguiente c√≥digo, haz clic en Verificar para comprobar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.

```Cpp
//Sound Control Light
int soundPin = A2; // Analog sound sensor is to be attached to analog
int ledPin = 4; // Digital LED is to be attached to digital
void setup() {
  pinMode(ledPin, OUTPUT);
  pinMode(soundPin, INPUT);
  Serial.begin(9600);
}
void loop(){
  int soundState = analogRead(soundPin); // Read sound sensor‚Äôs value
  Serial.println(soundState);
  // if the sound sensor‚Äôs value is greater than 400, the light will be on.
  //Otherwise, the light will be turned off
  if (soundState > 400) {
    digitalWrite(ledPin, HIGH);
    delay(100);
  }else{
    digitalWrite(ledPin, LOW);
  }
}
```

Tambi√©n puedes ver las lecturas de intensidad de luz desde el **Serial Plotter**, navega a **Tools** -> **Serial Plotter**.

**Nota: Tambi√©n puedes ajustar el valor seg√∫n la intensidad de luz de tu entorno.**

An√°lisis del C√≥digo

```cpp
Serial.begin(9600);
```

El software que se ejecuta en la computadora se comunica con la placa de desarrollo, y la velocidad de baudios es 9600.

```cpp
Serial.print(" ");
```

Esta funci√≥n se utiliza para enviar datos desde el puerto serie, la salida es lo que est√° contenido entre las comillas dobles.

```cpp
Serial.println( );
```

Esta declaraci√≥n es similar a la anterior, excepto que **serial.println** tiene un retorno de l√≠nea nueva.

```cpp
Serial.println(soundState);
```

El puerto serie imprime el valor del sensor de sonido. As√≠ que abres el **monitor serie** en la interfaz del IDE, y ves el valor del sensor de salida.

**Efecto de la Demo y Resultado de Impresi√≥n Serie:**

El m√≥dulo LED se encender√° si el entorno es lo suficientemente ruidoso.

Gu√≠a de Conexi√≥n

Usa cables Grove para conectar el Grove LED a la interfaz de se√±al digital **D4** del Seeeduino Lotus, Conecta el Grove Sensor de Sonido a la interfaz de se√±al anal√≥gica **A2** del Seeeduino Lotus.

### Lecci√≥n 7: Mostrando Datos en OLED

¬°La Pantalla OLED puede usarse para muchas situaciones, donde podr√≠as usarla para visualizar lecturas de sensores!

Informaci√≥n de Contexto:

- **Qu√© son las Librer√≠as de Arduino**

El entorno de Arduino puede extenderse mediante el uso de librer√≠as, al igual que la mayor√≠a de otras plataformas de programaci√≥n. Las librer√≠as proporcionan funcionalidades adicionales para usar en sketches, es decir, trabajar con hardware espec√≠fico o manipular datos. Para usar una librer√≠a en un sketch, selecci√≥nala desde **Sketch** ->**Include Library**.

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/zip.jpg" style={{width:500, height:'auto'}}/></div>

Para m√°s informaci√≥n, por favor visita tambi√©n [C√≥mo instalar Librer√≠as de Arduino](https://wiki.seeedstudio.com/es/How_to_install_Arduino_Library/).

Componentes Involucrados

    1. Seeeduino Lotus
    2. Grove OLED
    3. Cable Grove(Si est√° separado)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/OLED.png)

Conexi√≥n de Hardware

    - **Conexi√≥n del m√≥dulo:**
        - Conexi√≥n por defecto mediante orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora mediante un cable USB.

C√≥digo de Software

    - Abre Arduino IDE.
    - Instala la **librer√≠a U8g2**: Navega a **Sketch** -> **Include Library** -> **Manage Libraries...** y Busca la palabra clave "**U8g2**" en el **Library Manager**. Es la **librer√≠a u8g2 por oliver**, y haz clic luego instalar.

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/U8g2-lib.png)

- Copia el siguiente c√≥digo, haz clic en Verify para verificar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.

```cpp
#include <Arduino.h>
#include <U8x8lib.h>

 U8X8_SSD1306_128X64_NONAME_HW_I2C u8x8(/* reset=*/ U8X8_PIN_NONE);

// U8X8_SSD1306_128X64_NONAME_SW_I2C u8x8(/* clock=*/ SCL, /* data=*/ SDA, /* reset=*/ U8X8_PIN_NONE);   // OLEDs without Reset of the Display

void setup(void) {
  //u8x8.setBusClock(100000);  // If you breakout other modules, please enable this line
  u8x8.begin();
  u8x8.setFlipMode(1);
}

void loop(void) {
  u8x8.setFont(u8x8_font_chroma48medium8_r);
  u8x8.setCursor(0, 0);
  u8x8.print("Hello World!");
}
```

!!!Atenci√≥n
  Si separas todos los m√≥dulos y usas el OLED Grove por separado, puede que encuentres que no funciona con este c√≥digo. Si encuentras tal problema, por favor consulta el final de esta secci√≥n: Gu√≠a de Separaci√≥n.

- **An√°lisis del c√≥digo**

```cpp
#include <>
```

**Descripci√≥n:**

`#include` se utiliza para incluir bibliotecas externas en tu sketch. Esto le da al programador acceso a un gran grupo de bibliotecas est√°ndar de C (grupos de funciones pre-hechas), y tambi√©n bibliotecas escritas especialmente para Arduino.

Ten en cuenta que `#include`, similar a `#define`, no tiene terminador de punto y coma, y el compilador producir√° mensajes de error cr√≠pticos si a√±ades uno.

```cpp
#include <U8x8lib.h>
```

**#include** es una instrucci√≥n que introduce un archivo de cabecera. Aqu√≠ usamos la biblioteca U8x8lib.h.

```cpp
U8X8_SSD1306_128X64_NONAME_HW_I2C u8x8(/* reset=*/ U8X8_PIN_NONE);  
```

**Nota**
  
Si separas otros m√≥dulos y solo usas OLED, tienes que usar I2C por software:

```cpp
// U8X8_SSD1306_128X64_NONAME_HW_I2C u8x8(/* reset=*/ U8X8_PIN_NONE);  

U8X8_SSD1306_128X64_NONAME_SW_I2C u8x8(/* clock=*/ SCL, /* data=*/ SDA, /* reset=*/ U8X8_PIN_NONE); 
```

**Descripci√≥n:**

Una vez que el objeto est√° declarado, puedes usar funciones de la biblioteca.

```cpp
u8x8.begin();
```

**Descripci√≥n:**

Procedimiento de configuraci√≥n simplificado de la pantalla para el entorno Arduino. Consulte la gu√≠a de configuraci√≥n para la selecci√≥n de un constructor U8g2 adecuado.

**Sintaxis:**

u8x8.begin()

Inicializa la biblioteca u8g2

```cpp
u8x8.setFlipMode(1);
```

**Descripci√≥n:**

Algunas pantallas soportan una rotaci√≥n de 180 grados del b√∫fer de marco interno. Esta caracter√≠stica de hardware puede ser controlada con este procedimiento. Importante: Redibuja la pantalla completa despu√©s de cambiar el modo de volteo. Lo mejor es limpiar la pantalla primero, luego cambiar el modo de volteo y finalmente redibujar el contenido. Los resultados ser√°n indefinidos para cualquier contenido existente en la pantalla.

**Sintaxis:**

u8x8.setFlipMode(**mode**)

**Par√°metros:**

**mode**: `0` o `1`

Voltea la pantalla 180 grados.

```cpp
u8x8.setCursor();
```

**Descripci√≥n:**

Define el cursor para la funci√≥n de impresi√≥n. Cualquier salida de la funci√≥n de impresi√≥n comenzar√° en esta posici√≥n.

**Sintaxis:**

u8x8.setCursor(**x, y**)

**Par√°metros:**

**x, y**: Posici√≥n de columna/fila para el cursor de la funci√≥n de impresi√≥n.

Establece la posici√≥n del cursor de dibujo.

```cpp
u8x8.setFont()
```

**Descripci√≥n:**

Define una fuente u8x8 para las funciones de dibujo de glifos y cadenas.

**Sintaxis:**

u8x8.setFont(**font_8x8**)

Establece la fuente para la pantalla.

```cpp
u8x8.print();
```

Dibuja el contenido en el OLED.

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serie:**

Imprime Hello World en la Pantalla OLED.

**Referencia de la Biblioteca U8g2**

Si deseas m√°s informaci√≥n sobre la biblioteca U8g2, por favor consulta [aqu√≠](https://github.com/olikraus/u8g2/wiki/u8g2reference).

Gu√≠a de Conexi√≥n

Usa el cable Grove para conectar el OLED a la interfaz **I2C** del Seeeduino Lotus (Nota: La direcci√≥n predeterminada de I2C es 0x78).

!!!Note
    - Si conectas otro m√≥dulo para usar el OLED y no funciona, o quieres usar el I2C OLED m√°s r√°pido (predeterminado: 40KHZ), por favor sigue esta instrucci√≥n:

  Haz clic en "Este PC" -> Documentos -> Arduino -> libraries -> U8g2 -> src -> U8x8lib.cpp -> Desl√≠zate a la l√≠nea 1334 -> elimina o deshabilita esta l√≠nea -> guarda el archivo.

  ```CPP
  Wire.setClock(u8x8->bus_clock);   // just delete or disable this line
  ```

 O puedes configurar el reloj del bus a 100000 y luego a√±adirlo en el setup().

```cpp
void setup(void) {
  u8x8.setBusClock(100000); //  it for limit the I2C bus clock
  u8x8.begin();
  u8x8.setFlipMode(1);
}
```

### Lecci√≥n 8: Detectando Temperatura y Humedad del Entorno

¬øAlguna vez te has preguntado sobre la temperatura y humedad de tu entorno? ¬øQuieres saber el n√∫mero exacto? ¬øQuieres usar una falda o abrigo hoy dependiendo de la temperatura? ¬°Hagamos un medidor de temperatura!

Informaci√≥n de Contexto:

- **¬øQu√© es la Se√±al de Protocolo (I2C)?**

**Se√±al de protocolo:** la se√±al de protocolo que usamos es I2C, as√≠ que aqu√≠ hay una breve introducci√≥n a I2C. El bus I2C solo necesita dos cables en la transmisi√≥n de informaci√≥n conectando entre los dispositivos: el SDA (L√≠nea de Datos Serie) y SCL (L√≠nea de Reloj Serie).

Estas dos l√≠neas son l√≠neas I/O bidireccionales, el componente principal usado para iniciar la transferencia de datos del bus, y generar el reloj para abrir el dispositivo de transmisi√≥n, cualquier dispositivo que est√© siendo direccionado en este momento es considerado desde el dispositivo.

La relaci√≥n entre maestro y esclavo (emisor y receptor) en el bus no es constante sino que depende de la direcci√≥n de transmisi√≥n de datos. Si el host quiere enviar datos al dispositivo esclavo, el host primero direcciona el dispositivo esclavo, luego env√≠a activamente datos al dispositivo esclavo, y finalmente termina la transmisi√≥n de datos por el host. Si el host va a recibir datos del esclavo, el esclavo es primero direccionado por el maestro.

El host luego recibe los datos enviados desde el dispositivo, y el host termina el proceso de recepci√≥n. En este caso. El host es responsable de generar el reloj de temporizaci√≥n y terminar la transferencia de datos.

- **Pr√°ctica:** Haz que tu Pantalla OLED muestre la temperatura y humedad ambiental actual.

Componentes Involucrados

    1. Seeeduino Lotus
    2. Grove OLED
    3. Grove Sensor de Temperatura y Temperatura
    4. Grove cable(Si est√° separado)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Temp.png)

Conexi√≥n de Hardware

    - **Conexi√≥n de m√≥dulo:**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora v√≠a un cable USB.

**Nota**

Algunos kits actualizados han sido equipados con DHT20. Si tu Detector de Humedad y Temperatura en el kit es negro entonces el detector es DHT20 y el c√≥digo de ejemplo de este est√° despu√©s del DHT11.

C√≥digo de Software(DHT11)

    - Abre Arduino IDE.
    - Descarga e instala la [librer√≠a](https://github.com/Seeed-Studio/Grove_Temperature_And_Humidity_Sensor) requerida.
    - Copia el siguiente c√≥digo, haz clic en Verificar para revisar errores de sintaxis. Verifica que no hay errores, y puedes subir el c√≥digo.

```Cpp
//Temperature and Humidity Sensor
#include "DHT.h"
#include <Arduino.h>
#include <U8x8lib.h>

#define DHTPIN 3     // what pin we're connected to
#define DHTTYPE DHT11   // DHT 11 
DHT dht(DHTPIN, DHTTYPE);

U8X8_SSD1306_128X64_NONAME_HW_I2C u8x8(/* reset=*/ U8X8_PIN_NONE);

void setup(void) {
  Serial.begin(9600); 
  Serial.println("DHTxx test!");
  dht.begin();
  u8x8.begin();
  u8x8.setPowerSave(0);  
  u8x8.setFlipMode(1);
}

void loop(void) {

  float temp, humi;
  temp = dht.readTemperature();
  humi = dht.readHumidity();
  
  u8x8.setFont(u8x8_font_chroma48medium8_r);
  u8x8.setCursor(0, 33);
  u8x8.print("Temp:");
  u8x8.print(temp);
  u8x8.print("C");
  u8x8.setCursor(0,50);
  u8x8.print("Humidity:");
  u8x8.print(humi);
  u8x8.print("%");
  u8x8.refreshDisplay();
  delay(200);
}
```

C√≥digo de Software (DHT20)

    - Abre Arduino IDE.
    - Descarga e instala la [biblioteca](https://github.com/Seeed-Studio/Grove_Temperature_And_Humidity_Sensor) requerida.
    - Copia el siguiente c√≥digo, haz clic en Verificar para comprobar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.

```cpp
//Temperature and Humidity Sensor
#include "DHT.h"
#include <Arduino.h>
#include <U8x8lib.h>
#include "Wire.h"

#define DHTTYPE DHT20   // DHT 20

DHT dht(DHTTYPE);  
#if defined(ARDUINO_ARCH_AVR)
    #define debug  Serial
 
#elif defined(ARDUINO_ARCH_SAMD) ||  defined(ARDUINO_ARCH_SAM)
    #define debug  SerialUSB
#else
    #define debug  Serial
#endif
 
U8X8_SSD1306_128X64_NONAME_HW_I2C u8x8(/* reset=*/ U8X8_PIN_NONE);
 
void setup(void) {
    debug.begin(115200);
    debug.println("DHTxx test!");
    Wire.begin();
 
    /*if using WIO link,must pull up the power pin.*/
    // pinMode(PIN_GROVE_POWER, OUTPUT);
    // digitalWrite(PIN_GROVE_POWER, 1);
 
    dht.begin();
  dht.begin();
  u8x8.begin();
  u8x8.setPowerSave(0);  
  u8x8.setFlipMode(1);
}
 
void loop(void) {
 
  float temp, humi;
  temp = dht.readTemperature();
  humi = dht.readHumidity();
 
  u8x8.setFont(u8x8_font_chroma48medium8_r);
  u8x8.setCursor(0, 33);
  u8x8.print("Temp:");
  u8x8.print(temp);
  u8x8.print("C");
  u8x8.setCursor(0,50);
  u8x8.print("Humidity:");
  u8x8.print(humi);
  u8x8.print("%");
  u8x8.refreshDisplay();
  delay(200);
}
```

Haz clic en "Monitor" en la esquina superior derecha y verifica el resultado.

An√°lisis del C√≥digo

```cpp
float temp, humi;
```

Define variables para almacenar las lecturas.

```cpp
temp = dht.readTemperature();
humi = dht.readHumidity();
```

**Descripci√≥n:**

Funciones que se utilizan para leer los valores de temperatura y humedad del sensor.

**Sintaxis:**

**dht.readTemperature()** y **dht.readHumidity()**. Tipo de retorno: float.

Llama a estas funciones para leer la temperatura y humedad y almacenarlas en variables definidas.

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serial:**

La temperatura y humedad del entorno aparecen en la pantalla OLED.

Gu√≠a de Conexi√≥n

Usa el cable Grove para conectar el OLED a la interfaz **I2C** del Seeeduino Lotus (Nota: La direcci√≥n predeterminada de I2C es 0x78). Conecta el Sensor de Temperatura y Humedad Grove a la interfaz de se√±al digital **D3** del Seeeduino Lotus.

### Lecci√≥n 9: Midiendo la Presi√≥n del Aire Circundante

El Sensor de Presi√≥n de Aire Grove es una placa de conexi√≥n para medir la presi√≥n atmosf√©rica. La versi√≥n anterior (antes de octubre de 2025) usaba el sensor BMP280, mientras que la nueva versi√≥n (despu√©s de octubre de 2025) usa el sensor SPA06-003. Ambos sensores pueden medir con precisi√≥n la temperatura y la presi√≥n atmosf√©rica. Como la presi√≥n atmosf√©rica cambia con la altitud, tambi√©n pueden medir la altitud aproximada de un lugar.

Componentes Involucrados
    1. Seeeduino Lotus
    2. Sensor de Presi√≥n de Aire Grove (BMP280 o SPA06-003)
    3. Cable Grove(si est√° separado)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/AirPressure.png)

Conexi√≥n de hardware
    - **Conexi√≥n del m√≥dulo:**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora a trav√©s de un cable USB.

C√≥digo de Software

#### Para BMP280 (Versi√≥n anterior antes de octubre de 2025)

    - Abre Arduino IDE.
    - Instala la **biblioteca del Sensor Bar√≥metro Grove**: Navega a **Sketch** -> **Include Library** -> **Manage Libraries...** y Busca la palabra clave "**Grove BMP280**" en el **Library Manager**, luego instala.

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/BMP-lib.png)

- Copia el siguiente c√≥digo, haz clic en Verify para verificar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.
- En este programa, la informaci√≥n del sensor Bar√≥metro se env√≠a desde el sensor al Seeeduino a trav√©s del bus I2C y luego el Seeeduino las imprime en el monitor serial. Abre el **monitor serial** para verificar el resultado.

```cpp
//Air pressure detection
#include "Seeed_BMP280.h"
#include <Wire.h>

BMP280 bmp280;

void setup() {
    Serial.begin(9600);
    if (!bmp280.init()) {
        Serial.println("Device not connected or broken!");
    }
}

void loop() {

    float pressure;

    //get and print temperatures
    Serial.print("Temp: ");
    Serial.print(bmp280.getTemperature());
    Serial.println("C"); // The unit for  Celsius because original arduino don't support speical symbols

    //get and print atmospheric pressure data
    Serial.print("Pressure: ");
    Serial.print(pressure = bmp280.getPressure());
    Serial.println("Pa");

    //get and print altitude data
    Serial.print("Altitude: ");
    Serial.print(bmp280.calcAltitude(pressure));
    Serial.println("m");

    Serial.println("\n");//add a line between output of different times.

    delay(1000);
}
```

An√°lisis del C√≥digo

```cpp
#include <Wire.h>
```

**#include** es una instrucci√≥n que introduce un archivo de cabecera. Aqu√≠ usamos la biblioteca Wire.h, esta biblioteca est√° incluida en Arduino IDE.

```cpp
#include "Seeed_BMP280.h"
```

Representa el archivo de cabecera Seeed_BMP280.h que introduce la ruta actual.

```cpp
if (!bmp280.init()) {
    Serial.println("Device not connected or broken!");
}
```

**Descripci√≥n:**

Para inicializar el sensor de presi√≥n de aire usando bmp280.init(). Adem√°s, usando la condici√≥n if para verificar si se inici√≥ correctamente entonces omite el mensaje. Si tiene problemas para iniciar entonces imprime el mensaje como ! que significa NO en programaci√≥n.

**Sintaxis:**

**bmp280.init()**

si el sensor de presi√≥n de aire no se inici√≥ correctamente, entonces imprime un error al monitor serie.

```cpp
Serial.print(bmp280.getTemperature());
```

**Descripci√≥n:**

Funciones que se utilizan para leer el valor de temperatura del sensor.

**Sintaxis:**

**bmp280.getTemperature()**. Tipo de retorno: float

Imprime los datos de temperatura en el monitor serie.

```cpp
Serial.print(pressure = bmp280.getPressure());
```

**Descripci√≥n:**

Funciones que se utilizan para leer el valor de presi√≥n del aire del sensor.

**Sintaxis:**

**bmp280.getPressure()**. Tipo de retorno: float

Imprime la presi√≥n del aire actual.

```cpp
Serial.print(bmp280.calcAltitude(pressure));
```

**Descripci√≥n:**

Toma el valor de presi√≥n y puede convertirlo a altitud.

**Sintaxis:**

bmp280.calcAltitude(**float**). Tipo de retorno: float

**Par√°metro:**

**float**: Valor de presi√≥n.

Imprime la amplitud.

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serial:**

Las lecturas de presi√≥n del aire se muestran en el Monitor Serial.

Gu√≠a de Conexi√≥n

Usa un cable Grove para conectar el Sensor de Presi√≥n de Aire Grove a la interfaz **I2C** del Seeeduino Lotus usando un cable Grove (nota: la direcci√≥n I2C por defecto es 0x77 o 0x76).

#### Para SPA06-003 (Nueva versi√≥n despu√©s de octubre de 2025)

El SPA06-003 es un sensor de presi√≥n barom√©trica de alta precisi√≥n que puede medir tanto presi√≥n como temperatura. Aqu√≠ se explica c√≥mo usarlo con Arduino:

**Instalaci√≥n de Librer√≠a:**

- Descarga e instala la [librer√≠a SPL07-003](https://github.com/Seeed-Studio/Seeed_Arduino_SPA06) para Arduino

- La librer√≠a proporciona funciones para leer presi√≥n, temperatura y calcular altitud

**Descripci√≥n:** Este programa se interfaza con el sensor de presi√≥n y temperatura SPL07-003 para monitorear continuamente las condiciones atmosf√©ricas. Inicializa el sensor con configuraciones de muestreo espec√≠ficas (4Hz para presi√≥n con 32 muestras, 4Hz para temperatura con 1 muestra) y lee valores de presi√≥n, temperatura y altitud calculada en tiempo real, mostrando los resultados a trav√©s de comunicaci√≥n serial.

```cpp
#include <Wire.h>
#include "SPL07-003.h"

// Define SPL07-006 I2C address
#define SPL07_ADDR SPL07_ADDR_DEF // Default I2C address (SDO=high)
// #define SPL07_ADDR SPL07_ADDR_ALT // Alternate I2C address (SDO=low)

// Create SPL07-003 sensor instance
SPL07_003 spl;

//HardwareSerial SerialOut(PA10, PA9); //for STM32F103C8Tx

// Runs at startup
void setup() {

  // Begin Serial
  Serial.begin(115200);

  // Configure & start I2C
  //Wire.setSDA(PB7); //for STM32F103C8Tx
  //Wire.setSCL(PB6); //for STM32F103C8Tx
  Wire.begin();

  // Connect to SPL07-003
  if (spl.begin(SPL07_ADDR,&Wire) == false) {
    Serial.println("Error initializing SPL07-003 :(");
    while (1) {}
  }//if
  Serial.println("Connected to SPL07-003! :)");

  // Set pressure & temperature sampling settings
  spl.setPressureConfig(SPL07_4HZ, SPL07_32SAMPLES);
  spl.setTemperatureConfig(SPL07_4HZ, SPL07_1SAMPLE);

  // Set SPL07-003 to continuous measurements
  spl.setMode(SPL07_CONT_PRES_TEMP);

}//setup()


// Runs continuously
void loop() {

  // Wait for available reading
  if (spl.pressureAvailable() || spl.temperatureAvailable()) {
    // Read latest values
    double pres = spl.readPressure();
    double temp = spl.readTemperature();
    double altitude = spl.calcAltitude();
    // Print to serial
    Serial.print("Pres: ");
    Serial.print(pres, 3);
    Serial.print(" Pa, Temp: ");
    Serial.print(temp, 3);
    Serial.print(" C, Altitude: ");
    Serial.print(altitude, 3);
    Serial.println(" m");
  }//if

}//loop()
```

### Lecci√≥n 10: Detecci√≥n de Movimiento

Este es el √∫ltimo sensor, el aceler√≥metro triaxial, y con este m√≥dulo, puedes agregar f√°cilmente monitoreo de movimiento a tu dise√±o. As√≠ que podemos hacer muchos peque√±os experimentos interesantes basados en el movimiento.

- **Pr√°ctica:** cuando se detecta movimiento, el zumbador da una alarma indicando que el objeto est√° en movimiento.

Componentes Involucrados

    1. Seeeduino Lotus
    2. Grove Aceler√≥metro de 3 ejes
    3. Cable Grove(si est√° separado)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Gyro.png)

Conexi√≥n de hardware

    - **Conexi√≥n del m√≥dulo:**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora a trav√©s de un cable USB.

C√≥digo de Software

    - Abre Arduino IDE.
    - Descarga el [Aceler√≥metro Digital de 3 Ejes( ¬±2g a 16g)](https://github.com/Seeed-Studio/Seeed_Arduino_LIS3DHTR) desde Github. Haz clic en **Sketch** > **Include library** > **Add .ZIP library**, importa la librer√≠a al IDE.
    - Copia el siguiente c√≥digo, haz clic en Verify para verificar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.
    - En este programa, la informaci√≥n de aceleraci√≥n se env√≠a desde el sensor al Seeeduino a trav√©s del bus I2C y luego Seeeduino los imprime en el monitor serie. Abre el **monitor serie** para verificar el resultado.

```Cpp
//Gravity Acceleration
#include "LIS3DHTR.h"
#ifdef SOFTWAREWIRE
    #include <SoftwareWire.h>
    SoftwareWire myWire(3, 2);
    LIS3DHTR<SoftwareWire> LIS;       //Software I2C
    #define WIRE myWire
#else
    #include <Wire.h>
    LIS3DHTR<TwoWire> LIS;           //Hardware I2C
    #define WIRE Wire
#endif

void setup() {
    Serial.begin(9600);
    while (!Serial) {};
    LIS.begin(WIRE, 0x19); //IIC init
    delay(100);
    LIS.setOutputDataRate(LIS3DHTR_DATARATE_50HZ);
}
void loop() {
    if (!LIS) {
        Serial.println("LIS3DHTR didn't connect.");
        while (1);
        return;
    }
    //3 axis
    Serial.print("x:"); Serial.print(LIS.getAccelerationX()); Serial.print("  ");
    Serial.print("y:"); Serial.print(LIS.getAccelerationY()); Serial.print("  ");
    Serial.print("z:"); Serial.println(LIS.getAccelerationZ());

    delay(500);
}
```

An√°lisis del C√≥digo

```cpp
#include "LIS3DHTR.h"
#ifdef SOFTWAREWIRE
    #include <SoftwareWire.h>
    SoftwareWire myWire(3, 2);
    LIS3DHTR<SoftwareWire> LIS;   //Software I2C
    #define WIRE myWire
#else
    #include <Wire.h>
    LIS3DHTR<TwoWire> LIS;        //Hardware I2C
    #define WIRE Wire
#endif
```

Inicializando el m√≥dulo usando I2C por software o I2C por hardware.

```cpp
while (!Serial) {};
```

El c√≥digo se detiene aqu√≠ si no abres el monitor serie, as√≠ que abre el monitor serie.

```cpp
LIS.begin(WIRE, 0x19);
LIS.setOutputDataRate(LIS3DHTR_DATARATE_50HZ);
```

**Descripci√≥n:** Inicializa el aceler√≥metro.

**Sintaxis:** `LIS.begin(Wire, address)`.

**Descripci√≥n:** Establece la velocidad de datos de salida del aceler√≥metro.

**Sintaxis:** `LIS.setOutputDataRate(odr_type_t odr)`.

Inicializa el aceler√≥metro y establece la velocidad de salida a 50Hz.

```cpp
Serial.print("x:"); Serial.print(LIS.getAccelerationX()); Serial.print("  ");
Serial.print("y:"); Serial.print(LIS.getAccelerationY()); Serial.print("  ");
Serial.print("z:"); Serial.println(LIS.getAccelerationZ());
```

**Descripci√≥n:**

Funciones que se utilizan para leer el valor del eje X del sensor.

**Sintaxis:**

**LIS.getAccelerationX()**. Tipo de retorno: float.

**Descripci√≥n:**

Funciones que se utilizan para leer el valor del eje Y del sensor.

**Sintaxis:**

**LIS.getAccelerationY()**. Tipo de retorno: float.

**Descripci√≥n:**

Funciones que se utilizan para leer el valor del eje Z del sensor.

**Sintaxis:**

**LIS.getAccelerationZ()**. Tipo de retorno: float.

Imprime los datos de los 3 ejes en el monitor serie.

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serie:**

Las lecturas del aceler√≥metro de 3 ejes se muestran en el Monitor Serie.

Gu√≠a de Breakout

Usa un cable Grove para conectar el Aceler√≥metro de 3 ejes Grove a la interfaz **I2C** del Seeeduino Lotus usando un cable Grove (nota: la direcci√≥n I2C por defecto es 0x19).

## Proyectos Adicionales

### Proyecto 1: L√°mpara de ritmo din√°mico musical

- **Descripci√≥n del proyecto:** En este experimento, haremos que el buzzer reproduzca m√∫sica agradable y que las luces led parpadeen seg√∫n la frecuencia y el ritmo de la m√∫sica.

Componentes Involucrados

    1. Seeeduino Lotus
    2. LED Grove
    3. Buzzer
    4. Cables Grove(si est√°n separados)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/project1.png)

Conexi√≥n de hardware

    - **Conexi√≥n de m√≥dulos:**
        - Conexi√≥n por defecto mediante orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora mediante un cable USB.

C√≥digo de Software

    - Abre Arduino IDE.
    - Copia el siguiente c√≥digo, haz clic en Verificar para comprobar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.

```cpp
//Music Dynamic Rhythm Lamp
#define NTD0 -1
#define NTD1 294
#define NTD2 330
#define NTD3 350
#define NTD4 393
#define NTD5 441
#define NTD6 495
#define NTD7 556

#define NTDL1 147
#define NTDL2 165
#define NTDL3 175
#define NTDL4 196
#define NTDL5 221
#define NTDL6 248
#define NTDL7 278

#define NTDH1 589
#define NTDH2 661
#define NTDH3 700
#define NTDH4 786
#define NTDH5 882
#define NTDH6 990
#define NTDH7 112

#define WHOLE 1
#define HALF 0.5
#define QUARTER 0.25
#define EIGHTH 0.25
#define SIXTEENTH 0.625

int tune[]=
{
NTD3,NTD3,NTD4,NTD5,
NTD5,NTD4,NTD3,NTD2,
NTD1,NTD1,NTD2,NTD3,
NTD3,NTD2,NTD2,
NTD3,NTD3,NTD4,NTD5,
NTD5,NTD4,NTD3,NTD2,
NTD1,NTD1,NTD2,NTD3,
NTD2,NTD1,NTD1,
NTD2,NTD2,NTD3,NTD1,
NTD2,NTD3,NTD4,NTD3,NTD1,
NTD2,NTD3,NTD4,NTD3,NTD2,
NTD1,NTD2,NTDL5,NTD0,
NTD3,NTD3,NTD4,NTD5,
NTD5,NTD4,NTD3,NTD4,NTD2,
NTD1,NTD1,NTD2,NTD3,
NTD2,NTD1,NTD1
};

float durt[]=
{
1,1,1,1,
1,1,1,1,
1,1,1,1,
1+0.5,0.5,1+1,
1,1,1,1,
1,1,1,1,
1,1,1,1,
1+0.5,0.5,1+1,
1,1,1,1,
1,0.5,0.5,1,1,
1,0.5,0.5,1,1,
1,1,1,1,
1,1,1,1,
1,1,1,0.5,0.5,
1,1,1,1,
1+0.5,0.5,1+1,
};

int length;
int tonepin=5;
int ledp=4;

void setup()
{
  pinMode(tonepin,OUTPUT);
  pinMode(ledp,OUTPUT);
  length=sizeof(tune)/sizeof(tune[0]);
}

void loop()
{
  for(int x=0;x<length;x++)
  {
    tone(tonepin,tune[x]);
    digitalWrite(ledp, HIGH); 
    delay(400*durt[x]);
    digitalWrite(ledp, LOW);
    delay(100*durt[x]);
    noTone(tonepin);

  }
  delay(4000);
}
```

An√°lisis del C√≥digo
  
```cpp
#define NTD
```

Aqu√≠ est√° la definici√≥n de la frecuencia de la tecla D, que se divide en graves, medios y agudos.

```cpp
#define WHOLE 1
#define HALF 0.5
#define QUARTER 0.25
#define EIGHTH 0.25
#define SIXTEENTH 0.625
```

Nota: el ritmo se divide en un tiempo, medio tiempo, 1/4 de tiempo, 1/8 de tiempo, especificamos que el tiempo de una nota de un tiempo es 1; medio tiempo es 0.5; 1/4 de tiempo es 0.25; 1/8 es 0.125.

```cpp
int tune[]=...
```

Lista las frecuencias seg√∫n el espectro.

```cpp
float durt[]=...
```

Lista los beats seg√∫n el espectro.

```cpp
delay(100*durt[x]);
```

Controlar las luces LED encendidas y apagadas respectivamente.

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serie:**

El zumbador emitir√° una melod√≠a mientras el m√≥dulo LED parpadear√° con la misma frecuencia.

Gu√≠a de Conexi√≥n

Conecta el LED Grove a la interfaz de se√±al digital **D4** del Seeeduino Lotus, conecta el Zumbador a la interfaz de se√±al digital **D5** del Seeeduino Lotus.

### Proyecto 2: Hacer una l√°mpara de escritorio inteligente con inducci√≥n de sonido y luz

- **Descripci√≥n del proyecto:** como su nombre indica, este proyecto consiste en hacer una peque√±a l√°mpara controlada por Sonido y Luz. Necesitamos usar el m√≥dulo LED como salida. El sensor de luz y el sensor de sonido se usan para se√±ales de entrada. De esta manera, puedes lograr la funci√≥n de la l√°mpara de escritorio inteligente: si el nivel de sonido del entorno est√° por encima de cierto valor preestablecido, entonces el LED se enciende, o si la intensidad de luz del entorno est√° por debajo de cierto valor, el m√≥dulo LED tambi√©n se enciende.

Componentes Involucrados

    1. Seeeduino Lotus
    2. LED Grove
    3. Sensor de Luz
    4. Sensor de Sonido
    5. Cable Grove(Si se desconecta)

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/project2.png)

Conexi√≥n de Hardware

    - **Conexi√≥n del m√≥dulo:**
        - Conexi√≥n predeterminada por orificio de estampado PCB.
    - El Seeeduino se conecta luego a la computadora a trav√©s de un cable USB.

C√≥digo de Software

    - Abre Arduino IDE.
    - Copia el siguiente c√≥digo, haz clic en Verificar para comprobar errores de sintaxis. Verifica que no haya errores, y puedes subir el c√≥digo.

```Cpp
//light Induction Desk Lamp
int soundPin = A2; // Analog sound sensor is to be attached to analog
int lightPin = A6; //Analog light sensor is to be attached to analog
int ledPin = 4; // Digital LED is to be attached to digital

void setup() {
  pinMode(ledPin, OUTPUT);
  pinMode(lightPin, INPUT);
  pinMode(soundPin, INPUT);
}

void loop(){
  int soundState = analogRead(soundPin); // Read sound sensor‚Äôs value
  int lightState = analogRead(lightPin); // Read light sensor‚Äôs value
  // if the sound sensor's value is greater than 500 or the sound sensor's is less than 200, the light will be on.
  //Otherwise, the light will be turned off
if (soundState > 500 || lightState < 200) {
  digitalWrite(ledPin, HIGH);
  delay(500); //You can add the "//" to remove the delay
}else{
  digitalWrite(ledPin, LOW);
}
}
```

Code Analysis

```cpp
if (soundState > 500 || lightState < 200) {
  ...
}
```

En par√©ntesis hay una expresi√≥n l√≥gica. Tanto **&&** como **||** se usan com√∫nmente en expresiones l√≥gicas. El uso com√∫n es **if (expresi√≥n 1 || expresi√≥n 2)** e **if (expresi√≥n 1 && expresi√≥n 2)**.

**||** representa "**o**", satisface una de ellas, toda la expresi√≥n es verdadera, y satisface la condici√≥n del juicio if.

**&&** significa "**y**", la declaraci√≥n en if{} se ejecuta solo si todas las expresiones en par√©ntesis son verdaderas.

**Efecto de Demostraci√≥n y Resultado de Impresi√≥n Serial:**

Si el sonido circundante es lo suficientemente fuerte o la intensidad de luz es baja, el m√≥dulo LED se iluminar√° con m√°s intensidad.

Gu√≠a de Conexi√≥n

Conecta el LED Grove a la interfaz de se√±al digital **D4** del Seeeduino Lotus, Conecta el Sensor de Luz a la interfaz de se√±al anal√≥gica **A1** del Seeeduino Lotus. Conecta el Sensor de Sonido a la interfaz de se√±al anal√≥gica **A2** del Seeeduino Lotus usando un cable Grove.

## Crea Tus Propios M√≥dulos y Placas

Despu√©s de este per√≠odo de estudio, ya tienes una comprensi√≥n sistem√°tica de Arduino y hardware de c√≥digo abierto, as√≠ que ¬øpor qu√© no ir m√°s lejos y tratar de hacer tu propio m√≥dulo o placa de desarrollo?

### EDA

Para dise√±ar tu propia placa, necesitar√°s dise√±ar los esquem√°ticos de tu propio m√≥dulo, lo cual requiere una herramienta EDA para hacerlo. Aqu√≠ se recomienda un software EDA de c√≥digo abierto.

- **KiCAD**

[KiCad](https://www.kicad-pcb.org/) es una suite de software gratuito para automatizaci√≥n de dise√±o electr√≥nico. Facilita el dise√±o de esquem√°ticos para circuitos electr√≥nicos y su conversi√≥n a dise√±os de PCB. Cuenta con un entorno integrado para captura de esquem√°ticos y dise√±o de layout de PCB. Los programas manejan Captura de Esquem√°ticos y Dise√±o de Layout de PCB con salida Gerber. La suite funciona en Windows, Linux y macOS y est√° licenciada bajo GNU GPL v3.

- **Upverter**

Si no quieres trabajar en esquem√°ticos o layout t√∫ mismo, pero quieres convertir tu prototipo basado en m√≥dulos de Seeed en un producto integrado, te recomendamos encarecidamente que pruebes Upverter.

Por favor visita la [**Gu√≠a de Upverter del Kit para Principiantes Grove para Arduino**](https://wiki.seeedstudio.com/es/Grove-Beginner-Kit-for-Arduino-Upverter-Guide/) para m√°s informaci√≥n.

### Servicios de PCB

Despu√©s de que hayas terminado con tu dise√±o, echa un vistazo al [servicio PCBA de Seeed Fusion](https://www.seeedstudio.com/fusion_pcb.html), que puede ayudar a traducir tus dise√±os en un dispositivo real que funcione.

![](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/Fusion.png)

Seeed Studio tiene su propia [Biblioteca de Partes Abiertas (OPL)](https://www.seeedstudio.com/opl.html) que es una colecci√≥n de m√°s de 10,000 componentes com√∫nmente usados espec√≠ficamente obtenidos para el Servicio PCBA de Seeed Fusion. Para acelerar el proceso de dise√±o de PCB, Seeed est√° construyendo las bibliotecas de componentes para KiCad y Eagle. Cuando todos los componentes se obtienen de la OPL PCBA de Seeed y se usan con el [servicio de Ensamblaje de PCB de Seeed Fusion (PCBA)](https://www.seeedstudio.com/fusion_pcb.html), todo el tiempo de producci√≥n PCBA puede reducirse de 20 d√≠as laborales a solo 7 d√≠as.

## FAQ

### 1. ¬øC√≥mo remover los m√≥dulos electr√≥nicos individuales para su uso?

![image.png](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/remove1.jpg)

Mirando de cerca el Kit para Principiantes Grove Para Arduino ver√°s que hay 3 peque√±os agujeros entre cada m√≥dulo individual y la placa base. Todo lo que necesitas hacer es cortar la placa base del PCB alrededor del m√≥dulo desde los peque√±os agujeros usando un par de alicates diagonales.

![image.png](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/remove2.jpg)

---

**Nota: corta cuidadosamente a lo largo de la periferia del peque√±o agujero, no cortes hacia el peque√±o agujero (para prevenir cortocircuito del cableado interno y as√≠ da√±ar el m√≥dulo); si accidentalmente cortas hacia el peque√±o agujero, por favor usa un cuchillo para limpiar el peque√±o agujero para prevenir cortocircuito**

---

‚Äã![image.png](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/img/remove3.jpg)

## Visor Esquem√°tico en L√≠nea

<div className="altium-ecad-viewer" data-project-src="https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/res/Grove-Beginner-Kit-for-Arduino-SCH-PCB.zip" style={{borderRadius: '0px 0px 4px 4px', height: 500, borderStyle: 'solid', borderWidth: 1, borderColor: 'rgb(241, 241, 241)', overflow: 'hidden', maxWidth: 1280, maxHeight: 700, boxSizing: 'border-box'}}>
</div>

## Recursos

1. [**Grove Beginner Kit for Arduino Wiki [PDF]**](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/res/Grove-Beginner-Kit-For-ArduinoPDF.pdf)

2. Archivos de Dise√±o Esquem√°tico

    - [**Grove Beginner Kit for Arduino Archivos de Dise√±o Esquem√°tico V1.0 Versi√≥n de Vista Previa**](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/res/Grove-Beginner-Kit-for-Arduino-SCH-PCB.zip)

    - [**Grove Beginner Kit for Arduino Archivos de Dise√±o Esquem√°tico V1.1 Nueva Versi√≥n**](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/res/Grove_Beginner_Kit_for_Arduino_v2.3_SCH&PCB_250812.zip)

3. **Librer√≠as de M√≥dulos en Github:**

    - [Pantalla OLED](https://github.com/olikraus/U8g2_Arduino)
    - [Sensor de Temperatura y Humedad](https://github.com/Seeed-Studio/Grove_Temperature_And_Humidity_Sensor)
    - [Sensor de Presi√≥n de Aire (BMP280)](https://github.com/Seeed-Studio/Grove_BMP280)
    - [Sensor de Presi√≥n de Aire (SPA06-003)](https://github.com/Seeed-Studio/Seeed_Arduino_SPA06)
    - [Aceler√≥metro de 3 Ejes](https://github.com/Seeed-Studio/Seeed_Arduino_LIS3DHTR)

4. [**Hoja de Datos de Sensores**](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/res/Grove-beginner-kit-for-arduino-datasheet.zip)

5. [**Demo de Firmware Inicial de Arduino**](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/res/GroveBeginnerKitFirmwareFINAL.zip)

6. [**Grove Beginner Kit For Arduino Recursos en uno [20200401] (7z)**](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/res/Grove-Beginner-Kit-For-Arduino-Resources-in-one(20200401).7z)

7. [**Grove Beginner Kit For Arduino Curso de Programaci√≥n Gr√°fica Codecraft web v7**](https://files.seeedstudio.com/wiki/Grove-Beginner-Kit-For-Arduino/res/Grove-Beginner-Kit-For-Arduino-Codecraft-Graphical-Programming-Course-web-v7.pdf)

## M√°s Aprendizaje

- [LSTM para predicci√≥n de datos IoT en vivo](https://github.com/256ericpan/LSTM_IoT)

- [Gu√≠a para Principiantes de Programaci√≥n Sin C√≥digo para Biolog√≠a](https://www.biomaker.org/nocode-programming-for-biology-handbook)

## Soporte T√©cnico y Discusi√≥n de Productos

¬°Gracias por elegir nuestros productos! Estamos aqu√≠ para brindarle diferentes tipos de soporte para asegurar que su experiencia con nuestros productos sea lo m√°s fluida posible. Ofrecemos varios canales de comunicaci√≥n para satisfacer diferentes preferencias y necesidades.

<div class="button_tech_support_container">
<a href="https://forum.seeedstudio.com/" class="button_forum"></a>
<a href="https://www.seeedstudio.com/contacts" class="button_email"></a>
</div>

<div class="button_tech_support_container">
<a href="https://discord.gg/eWkprNDMU7" class="button_discord"></a>
<a href="https://github.com/Seeed-Studio/wiki-documents/discussions/69" class="button_discussion"></a>
</div>
